[[algorithms-triangle-count]]
= Triangle Count
:entity: node
:result: triangle count
:algorithm: Triangle Count

[abstract]
--
This section describes the Triangle Count algorithm in the Neo4j Graph Data Science library.
--

This topic includes:

* <<algorithms-triangle-count-intro, Introduction>>
* <<algorithms-triangle-count-syntax, Syntax>>
** <<algorithms-triangle-count-syntax-anonymous, Anonymous graphs>>
** <<algorithms-triangle-count-syntax-triangles-listing, Triangles Listing>>
* <<algorithms-triangle-count-examples, Examples>>
** <<algorithms-triangle-count-examples-memory-estimation, Memory Estimation>>
** <<algorithms-triangle-count-examples-stream, Stream>>
** <<algorithms-triangle-count-examples-stats, Stats>>
** <<algorithms-triangle-count-examples-mutate, Mutate>>
** <<algorithms-triangle-count-examples-write, Write>>
** <<algorithms-triangle-count-examples-max-degree, Maximum degree>>
** <<algorithms-triangle-count-examples-triangles-listing, Triangles Listing>>


[[algorithms-triangle-count-intro]]
== Introduction

The Triangle Count algorithm counts the number of triangles for each node in the graph.
A triangle is a set of three nodes where each node has a relationship to the other two.
In graph theory terminology, this is sometimes referred to as a 3-clique.
The Triangle Count algorithm in the GDS library only finds triangles in undirected graphs.

Triangle counting has gained popularity in social network analysis, where it is used to detect communities and measure the cohesiveness of those communities.
It can also be used to determine the stability of a graph, and is often used as part of the computation of network indices, such as clustering coefficients.
The Triangle Count algorithm is also used to compute the <<algorithms-local-clustering-coefficient, Local Clustering Coefficient>>.

For more information on this algorithm, see:

* Triangle count and clustering coefficient have been shown to be useful as features for classifying a given website as spam, or non-spam, content.
This is described in http://chato.cl/papers/becchetti_2007_approximate_count_triangles.pdf["Efficient Semi-streaming Algorithms for Local Triangle Counting in Massive Graphs"].


[[algorithms-triangle-count-syntax]]
== Syntax

include::../shared/syntax-intro-named-graph.adoc[]

WARNING: The named graphs must be projected in the `UNDIRECTED` orientation for the Triangle Count algorithm.


.Triangle Count syntax per mode
[.tabbed-example]
====

[.include-with-stream]
======
.Run Triangle Count in stream mode on a named graph:
[source, cypher]
----
CALL gds.triangleCount.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  triangleCount: Integer
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type      | Description
| nodeId        | Integer   | Node ID.
| triangleCount | Integer   | Number of triangles the node is part of. Is `-1` if the node has been excluded from computation using the `maxDegree` configuration parameter.
|===
======

[.include-with-stats]
======
.Run Triangle Count in stats mode on a named graph:
[source, cypher]
----
CALL gds.triangleCount.stats(
  graphName: String,
  configuration: Map
)
YIELD
  globalTriangleCount: Integer,
  nodeCount: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                | Type    | Description
| globalTriangleCount | Integer | Total number of triangles in the graph.
| nodeCount           | Integer | Number of nodes in the graph.
| createMillis        | Integer | Milliseconds for creating the graph.
| computeMillis       | Integer | Milliseconds for running the algorithm.
| configuration       | Map     | The configuration used for running the algorithm.
|===
======

[.include-with-mutate]
======
.Run Triangle Count in mutate mode on a named graph:
[source, cypher]
----
CALL gds.triangleCount.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  globalTriangleCount: Integer,
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                        | Type    | Description
| globalTriangleCount         | Integer | Total number of triangles in the graph.
| nodeCount                   | Integer | Number of nodes in the graph.
| nodePropertiesWritten       | Integer | Number of properties added to the in-memory graph.
| createMillis                | Integer | Milliseconds for creating the graph.
| computeMillis               | Integer | Milliseconds for running the algorithm.
| mutateMillis                | Integer | Milliseconds for adding properties to the in-memory graph.
| configuration               | Map     | The configuration used for running the algorithm.
|===
======

[.include-with-write]
======
.Run Triangle Count in write mode on a named graph:
[source, cypher]
----
CALL gds.triangleCount.write(
  graphName: String,
  configuration: Map
)
YIELD
  globalTriangleCount: Integer,
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                        | Type    | Description
| globalTriangleCount         | Integer | Total number of triangles in the graph.
| nodeCount                   | Integer | Number of nodes in the graph.
| nodePropertiesWritten       | Integer | Number of properties written to Neo4j.
| createMillis                | Integer | Milliseconds for creating the graph.
| computeMillis               | Integer | Milliseconds for running the algorithm.
| writeMillis                 | Integer | Milliseconds for writing results back to Neo4j.
| configuration               | Map     | The configuration used for running the algorithm.
|===
======
====


[[algorithms-triangle-count-syntax-anonymous]]
=== Anonymous graphs

include::../shared/syntax-anonymous-graphs.adoc[]

.Run Triangle Count in write mode on an anonymous graph:
[source, cypher]
----
CALL gds.triangleCount.write(
  configuration: Map
)
YIELD
  globalTriangleCount: Integer,
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-write-configuration-anonymous-graph.adoc[]

include::specific-configuration.adoc[]

The results are the same as for running write mode with a named graph, see the <<algorithms-triangle-count-syntax, write mode syntax above>>.


[[algorithms-triangle-count-syntax-triangles-listing]]
=== Triangles listing

In addition to the standard execution modes there is an `alpha` procedure `gds.alpha.triangles` that can be used to list all triangles in the graph.

include::../alpha/alpha-note.adoc[]

.The following will return a stream of node IDs for each triangle:
[source, cypher]
----
CALL gds.alpha.triangles(
  graphName: String,
  configuration: Map
)
YIELD nodeA, nodeB, nodeC
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name  | Type    | Description
| nodeA | Integer | The ID of the first node in the given triangle.
| nodeB | Integer | The ID of the second node in the given triangle.
| nodeC | Integer | The ID of the third node in the given triangle.
|===


[[algorithms-triangle-count-examples]]
== Examples

In this section we will show examples of executing the Triangle Count algorithm on a concrete graph.
The intention is to illustrate what the results look like and to provide a guide in how to make use of the algorithm in a real setting.
We will do this on a small social network graph of a handful nodes connected in a particular pattern.
The example graph looks like this:

[[algorithms-triangle-count-examples-graph]]
image::triangle_count.png[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=setup-query]
----
CREATE
  (alice:Person {name: 'Alice'}),
  (michael:Person {name: 'Michael'}),
  (karin:Person {name: 'Karin'}),
  (chris:Person {name: 'Chris'}),
  (will:Person {name: 'Will'}),
  (mark:Person {name: 'Mark'}),

  (michael)-[:KNOWS]->(karin),
  (michael)-[:KNOWS]->(chris),
  (will)-[:KNOWS]->(michael),
  (mark)-[:KNOWS]->(michael),
  (mark)-[:KNOWS]->(will),
  (alice)-[:KNOWS]->(michael),
  (will)-[:KNOWS]->(chris),
  (chris)-[:KNOWS]->(karin)
----

With the graph in Neo4j we can now project it into the graph catalog to prepare it for algorithm execution.
We do this using a native projection targeting the `Person` nodes and the `KNOWS` relationships.
For the relationships we must use the `UNDIRECTED` orientation.
This is because the Triangle Count algorithm is defined only for undirected graphs.

include::../shared/examples-named-native-note.adoc[]

.The following statement will create a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=graph-create-query]
----
CALL gds.graph.create(
  'myGraph',
  'Person',
  {
    KNOWS: {
      orientation: 'UNDIRECTED'
    }
  }
)
----

WARNING: The Triangle Count algorithm requires the graph to be created using the `UNDIRECTED` orientation for relationships.

In the following examples we will demonstrate using the Triangle Count algorithm on this graph.


[[algorithms-triangle-count-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::../shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm in write mode:
[source, cypher]
----
CALL gds.triangleCount.write.estimate('myGraph', { writeProperty: 'triangleCount' })
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 16                | 144      | 144      | "144 Bytes"
|===
--

Note that the relationship count is 16 although we only created 8 relationships in the original Cypher statement.
This is because we used the `UNDIRECTED` orientation, which will project each relationship in each direction, effectively doubling the number of relationships.


[[algorithms-triangle-count-examples-stream]]
=== Stream

:stream-details: For example, we can order the results to find the nodes with the highest triangle count.
include::../shared/examples-stream-intro.adoc[]

.The following will run the algorithm in `stream` mode:
[role=query-example]
--
[source, cypher]
----
CALL gds.triangleCount.stream('myGraph')
YIELD nodeId, triangleCount
RETURN gds.util.asNode(nodeId).name AS name, triangleCount
ORDER BY triangleCount DESC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | triangleCount
| "Michael" | 3
| "Chris"   | 2
| "Will"    | 2
| "Karin"   | 1
| "Mark"    | 1
| "Alice"   | 0
|===
--

Here we find that the 'Michael' node has the most triangles.
This can be verified in the <<algorithms-triangle-count-examples-graph, example graph>>.
Since the 'Alice' node only `KNOWS` one other node, it can not be part of any triangle, and indeed the algorithm reports a count of zero.


[[algorithms-triangle-count-examples-stats]]
=== Stats

:stats-details: The summary result contains the global triangle count, which is the total number of triangles in the entire graph.
:stats-syntax: algorithms-triangle-count-syntax
include::../shared/examples-stats-intro.adoc[]

.The following will run the algorithm in `stats` mode:
[role=query-example]
--
[source, cypher]
----
CALL gds.triangleCount.stats('myGraph')
YIELD globalTriangleCount, nodeCount
----

.Results
[opts="header"]
|===
| globalTriangleCount | nodeCount
| 3                   | 6
|===
--

Here we can see that the graph has six nodes with a total number of three triangles.
Comparing this to the <<algorithms-triangle-count-examples-stream, stream example>> we can see that the 'Michael' node has a triangle count equal to the global triangle count.
In other words, that node is part of all of the triangles in the graph and thus has a very central position in the graph.


[[algorithms-triangle-count-examples-mutate]]
=== Mutate

:mutate-details: For example, using the triangle count to compute the <<algorithms-local-clustering-coefficient-examples-pre-computed, local clustering coefficient>>.
include::../shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher]
----
CALL gds.triangleCount.mutate('myGraph', {
  mutateProperty: 'triangles'
})
YIELD globalTriangleCount, nodeCount
----

.Results
[opts="header"]
|===
| globalTriangleCount | nodeCount
| 3                   | 6
|===
--

The returned result is the same as in the `stats` example.
Additionally, the graph 'myGraph' now has a node property `triangles` which stores the triangle count for each node.
To find out how to inspect the new schema of the in-memory graph, see <<catalog-graph-list>>.


[[algorithms-triangle-count-examples-write]]
=== Write

include::../shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher]
----
CALL gds.triangleCount.write('myGraph', {
  writeProperty: 'triangles'
})
YIELD globalTriangleCount, nodeCount
----

.Results
[opts="header"]
|===
| globalTriangleCount | nodeCount
| 3                   | 6
|===
--

The returned result is the same as in the `stats` example.
Additionally, each of the six nodes now has a new property `triangles` in the Neo4j database, containing the triangle count for that node.


[[algorithms-triangle-count-examples-max-degree]]
=== Maximum Degree

The Triangle Count algorithm supports a `maxDegree` configuration parameter that can be used to exclude nodes from processing if their degree is greater than the configured value.
This can be useful to speed up the computation when there are nodes with a very high degree (so-called super nodes) in the graph.
Super nodes have a great impact on the performance of the Triangle Count algorithm.
To learn about the degree distribution of your graph, see <<catalog-graph-list>>.

The nodes excluded from the computation get assigned a triangle count of `-1`.

.The following will run the algorithm in `stream` mode with the `maxDegree` parameter:
[role=query-example]
--
[source, cypher]
----
CALL gds.triangleCount.stream('myGraph', {
  maxDegree: 4
})
YIELD nodeId, triangleCount
RETURN gds.util.asNode(nodeId).name AS name, triangleCount
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | triangleCount
| "Alice"   | 0
| "Chris"   | 0
| "Karin"   | 0
| "Mark"    | 0
| "Michael" | -1
| "Will"    | 0
|===
--

Running the algorithm on the example graph with `maxDegree: 4` excludes the 'Michael' node from the computation, as it has a degree of 5.

As this node is part of all the triangles in the example graph excluding it results in no triangles.


[[algorithms-triangle-count-examples-triangles-listing]]
== Triangles listing

It is also possible to list all the triangles in the graph.
To do this we make use of the `alpha` procedure `gds.alpha.triangles`.

include::../alpha/alpha-note.adoc[]

[role=query-example]
--
.The following will compute a stream of node IDs for each triangle and return the name property of the nodes:
[source, cypher]
----
CALL gds.alpha.triangles('myGraph')
YIELD nodeA, nodeB, nodeC
RETURN
  gds.util.asNode(nodeA).name AS nodeA,
  gds.util.asNode(nodeB).name AS nodeB,
  gds.util.asNode(nodeC).name AS nodeC
----

.Results
[opts="header"]
|===
| nodeA     | nodeB   | nodeC
| "Michael" | "Karin" | "Chris"
| "Michael" | "Chris" | "Will"
| "Michael" | "Will"  | "Mark"
|===
--

We can see that there are three triangles in the graph: "Will, Michael, and Chris", "Will, Mark, and Michael", and "Michael, Karin, and Chris".
The node "Alice" is not part of any triangle and thus does not appear in the triangles listing.
