[[algorithms-wcc]]
= Weakly Connected Components
:entity: node
:result: component ID
:algorithm: Weakly Connected Components

[abstract]
--
This section describes the Weakly Connected Components (WCC) algorithm in the Neo4j Graph Data Science library.
--

This topic includes:

* <<algorithms-wcc-intro, Introduction>>
* <<algorithms-wcc-syntax, Syntax>>
** <<algorithms-wcc-syntax-anonymous-graphs, Anonymous graphs>>
* <<algorithms-wcc-examples, Examples>>
** <<algorithms-wcc-examples-memory-estimation, Memory Estimation>>
** <<algorithms-wcc-examples-stream, Stream>>
** <<algorithms-wcc-examples-stats, Stats>>
** <<algorithms-wcc-examples-mutate, Mutate>>
** <<algorithms-wcc-examples-write, Write>>
** <<algorithms-wcc-examples-weighted, Weighted>>
** <<algorithms-wcc-examples-seeding, Seeded components>>
** <<algorithms-wcc-examples-seed-write, Writing Seeded components>>


[[algorithms-wcc-intro]]
== Introduction

The WCC algorithm finds sets of connected nodes in an undirected graph, where all nodes in the same set form a connected component.
WCC is often used early in an analysis to understand the structure of a graph.
Using WCC to understand the graph structure enables running other algorithms independently on an identified cluster.
As a preprocessing step for directed graphs, it helps quickly identify disconnected groups.

For more information on this algorithm, see:

* http://citeseerx.ist.psu.edu/viewdoc/summary?doi=10.1.1.28.8405["An efficient domain-independent algorithm for detecting approximately duplicate database records"^].
* One study uses WCC to work out how well connected the network is, and then to see whether the connectivity remains if 'hub' or 'authority' nodes are moved from the graph: https://link.springer.com/article/10.1007%2Fs10115-003-0128-3["Characterizing and Mining Citation Graph of Computer Science Literature"^]

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-estimation>>.
====


[[algorithms-wcc-syntax]]
== Syntax

include::../shared/syntax-intro-named-graph.adoc[]


.WCC syntax per mode
[.tabbed-example]
====

[.include-with-stream]
======
.Run WCC in stream mode on a named graph.
[source, cypher]
----
CALL gds.wcc.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  componentId: Integer
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name        | Type    | Description
| nodeId      | Integer | Node ID.
| componentId | Integer | Component ID.
|===
======


[.include-with-stats]
======
.Run WCC in stats mode on a named graph.
[source, cypher]
----
CALL gds.wcc.stats(
  graphName: String,
  configuration: Map
)
YIELD
  componentCount: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  componentDistribution: Map,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type    | Description
| componentCount        | Integer | The number of computed components.
| createMillis          | Integer | Milliseconds for loading data.
| computeMillis         | Integer | Milliseconds for running the algorithm.
| postProcessingMillis  | Integer | Milliseconds for computing component count and distribution statistics.
| componentDistribution | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of component sizes.
| configuration         | Map     | The configuration used for running the algorithm.
|===
======


[.include-with-mutate]
======
.Run WCC in mutate mode on a named graph.
[source, cypher]
----
CALL gds.wcc.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  componentCount: Integer,
  nodePropertiesWritten: Integer,
  relationshipPropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  postProcessingMillis: Integer,
  componentDistribution: Map,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                          | Type    | Description
| componentCount                | Integer | The number of computed components.
| nodePropertiesWritten         | Integer | The number of node properties written.
| relationshipPropertiesWritten | Integer | The number of relationship properties written.
| createMillis                  | Integer | Milliseconds for loading data.
| computeMillis                 | Integer | Milliseconds for running the algorithm.
| mutateMillis                  | Integer | Milliseconds for adding properties to the in-memory graph.
| postProcessingMillis          | Integer | Milliseconds for computing component count and distribution statistics.
| componentDistribution         | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of component sizes.
| configuration                 | Map     | The configuration used for running the algorithm.
|===
======


[.include-with-write]
======
.Run WCC in write mode on a named graph.
[source, cypher]
----
CALL gds.wcc.write(
  graphName: String,
  configuration: Map
)
YIELD
  componentCount: Integer,
  nodePropertiesWritten: Integer,
  relationshipPropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  postProcessingMillis: Integer,
  componentDistribution: Map,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-write-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                          | Type    | Description
| componentCount                | Integer | The number of computed components.
| nodePropertiesWritten         | Integer | The number of node properties written.
| relationshipPropertiesWritten | Integer | The number of relationship properties written.
| createMillis                  | Integer | Milliseconds for loading data.
| computeMillis                 | Integer | Milliseconds for running the algorithm.
| writeMillis                   | Integer | Milliseconds for writing result back to Neo4j.
| postProcessingMillis          | Integer | Milliseconds for computing component count and distribution statistics.
| componentDistribution         | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of component sizes.
| configuration                 | Map     | The configuration used for running the algorithm.
|===
======
====


[[algorithms-wcc-syntax-anonymous-graphs]]
=== Anonymous graphs

include::../shared/syntax-anonymous-graphs.adoc[]

.Run WCC in write mode on an anonymous graph:
[source, cypher]
----
CALL gds.wcc.write(
  configuration: Map
)
YIELD
  componentCount: Integer,
  nodePropertiesWritten: Integer,
  relationshipPropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  postProcessingMillis: Integer,
  componentDistribution: Map,
  configuration: Map
----

include::../common-configuration/common-write-configuration-anonymous-graph.adoc[]

include::specific-configuration.adoc[]

The results are the same as for running write mode with a named graph, see the <<algorithms-wcc-syntax, write mode syntax above>>.


[[algorithms-wcc-examples]]
== Examples

In this section we will show examples of executing the Weakly Connected Components algorithm on a concrete graph.
The intention is to illustrate what the results look like and to provide a guide in how to make use of the algorithm in a real setting.
We will do this on a small social network graph of a handful nodes connected in a particular pattern.
The example graph looks like this:

[[algorithms-wcc-examples-graph]]
image::wcc-graph.svg[align="center"]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=setup-query]
----
CREATE
  (nAlice:User {name: 'Alice'}),
  (nBridget:User {name: 'Bridget'}),
  (nCharles:User {name: 'Charles'}),
  (nDoug:User {name: 'Doug'}),
  (nMark:User {name: 'Mark'}),
  (nMichael:User {name: 'Michael'}),

  (nAlice)-[:LINK {weight: 0.5}]->(nBridget),
  (nAlice)-[:LINK {weight: 4}]->(nCharles),
  (nMark)-[:LINK {weight: 1.1}]->(nDoug),
  (nMark)-[:LINK {weight: 2}]->(nMichael);
----

This graph has two connected components, each with three nodes.
The relationships that connect the nodes in each component have a property `weight` which determines the strength of the relationship.

include::../shared/examples-named-native-note.adoc[]

.The following statement will create a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=graph-create-query]
----
CALL gds.graph.create(
  'myGraph',
  'User',
  'LINK',
  {
    relationshipProperties: 'weight'
  }
)
----

In the following examples we will demonstrate using the Weakly Connected Components algorithm on this graph.

[[algorithms-wcc-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::../shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm in write mode:
[source, cypher]
----
CALL gds.wcc.write.estimate('myGraph', { writeProperty: 'component' })
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 4                 | 176      | 176      | "176 Bytes"
|===
--


[[algorithms-wcc-examples-stream]]
=== Stream

:stream-details: For example, we can order the results to see the nodes that belong to the same component displayed next to each other.
include::../shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.wcc.stream('myGraph')
YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS name, componentId
ORDER BY componentId, name
----

.Results
[opts="header"]
|===
| name      | componentId
| "Alice"   | 0
| "Bridget" | 0
| "Charles" | 0
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===
--

The result shows that the algorithm identifies two components.
This can be verified in the <<algorithms-wcc-examples-graph, example graph>>.

The default behaviour of the algorithm is to run `unweighted`, e.g. without using `relationship` weights.
The `weighted` option will be demonstrated in <<algorithms-wcc-examples-weighted>>

[[algorithms-wcc-examples-stats]]
=== Stats

:stats-syntax: algorithms-wcc-syntax
include::../shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stats` mode:
[source, cypher]
----
CALL gds.wcc.stats('myGraph')
YIELD componentCount
----

.Results
[opts="header"]
|===
| componentCount
| 2
|===
--

The result shows that `myGraph` has two components and this can be verified by looking at the <<algorithms-wcc-examples-graph, example graph>>.


[[algorithms-wcc-examples-mutate]]
=== Mutate

include::../shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher]
----
CALL gds.wcc.mutate('myGraph', { mutateProperty: 'componentId' })
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header"]
|===
| nodePropertiesWritten | componentCount
| 6                     | 2
|===
--

[[algorithms-wcc-examples-write]]
=== Write

include::../shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher]
----
CALL gds.wcc.write('myGraph', { writeProperty: 'componentId' })
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header"]
|===
| nodePropertiesWritten | componentCount
| 6                     | 2
|===
--

As we can see from the results, the nodes connected to one another are calculated by the algorithm as belonging to the same connected component.


[[algorithms-wcc-examples-weighted]]
=== Weighted

By configuring the algorithm to use a weight we can increase granularity in the way the algorithm calculates component assignment.
We do this by specifying the property key with the `relationshipWeightProperty` configuration parameter.
Additionally, we can specify a threshold for the weight value.
Then, only weights greater than the threshold value will be considered by the algorithm.
We do this by specifying the threshold value with the `threshold` configuration parameter.

If a relationship does not have the specified weight property, the algorithm falls back to using a default value.
The default fallback value is zero, but can be configured to using the `defaultValue` configuration parameter.

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.wcc.stream('myGraph', {
  relationshipWeightProperty: 'weight',
  threshold: 1.0
}) YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS Name, componentId AS ComponentId
ORDER BY ComponentId, Name
----

.Results
[opts="header",cols="1m,1m"]
|===
| Name      | ComponentId
| "Alice"   | 0
| "Charles" | 0
| "Bridget" | 1
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===
--

As we can see from the results, the node named 'Bridget' is now in its own component, due to its relationship weight being less than the configured threshold and thus ignored.

NOTE: We are using stream mode to illustrate running the algorithm as weighted or unweighted, all the other  algorithm modes also support this configuration parameter.

[[algorithms-wcc-examples-seeding]]
=== Seeded components

It is possible to define preliminary component IDs for nodes using the `seedProperty` configuration parameter.
This is helpful if we want to retain components from a previous run and it is known that no components have been split by removing relationships.
The property value needs to be a number.

The algorithm first checks if there is a seeded component ID assigned to the node.
If there is one, that component ID is used.
Otherwise, a new unique component ID is assigned to the node.

Once every node belongs to a component, the algorithm merges components of connected nodes.
When components are merged, the resulting component is always the one with the lower component ID.
Note that the `consecutiveIds` configuration option cannot be used in combination with seeding in order to retain the seeding values.

[NOTE]
====
The algorithm assumes that nodes with the same seed value do in fact belong to the same component.
If any two nodes in different components have the same seed, behavior is undefined.
It is then recommended running WCC without seeds.
====

[[algorithms-wcc-examples-seeding-steps]]
To demonstrate this in practice, we will go through a few steps:

. We will run the algorithm and write the results to Neo4j.
. Then we will add another node to our graph, this node will not have the property computed in Step 1.
. We will create a new in-memory graph that has the result from Step 1 as `nodeProperty`
. And then we will run the algorithm again, this time in `stream` mode, and we will use the `seedProperty` configuration parameter.

We will use the weighted variant of WCC.

_Step 1_
[role=query-example, group=seeding]
--
.The following will run the algorithm in `write` mode:
[source, cypher]
----
CALL gds.wcc.write('myGraph', {
  writeProperty: 'componentId',
  relationshipWeightProperty: 'weight',
  threshold: 1.0
})
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header"]
|===
| nodePropertiesWritten | componentCount
| 6                     | 3
|===
--

_Step 2_

After the algorithm has finished writing to Neo4j we want to create a new node in the database.

[role=query-example, no-result=true, group=seeding]
--
.The following will create a new node in the Neo4j graph, with no component ID:
[source, cypher]
----
MATCH (b:User {name: 'Bridget'})
CREATE (b)-[:LINK {weight: 2.0}]->(new:User {name: 'Mats'})
----
--

_Step 3_

Note, that we cannot use our already created graph as it does not contain the component id.
We will therefore create a second in-memory graph that contains the previously computed component id.

[role=query-example, no-result=true, group=seeding]
--
.The following will create a new graph containing the previously computed component id:
[source, cypher]
----
CALL gds.graph.create(
  'myGraph-seeded',
  'User',
  'LINK',
  {
    nodeProperties: 'componentId',
    relationshipProperties: 'weight'
  }
)
----
--

_Step 4_


[role=query-example, group=seeding]
--
.The following will run the algorithm in `stream` mode using `seedProperty`:
[source, cypher]
----
CALL gds.wcc.stream('myGraph-seeded', {
  seedProperty: 'componentId',
  relationshipWeightProperty: 'weight',
  threshold: 1.0
}) YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS name, componentId
ORDER BY componentId, name
----

.Results
[opts="header"]
|===
| name      | componentId
| "Alice"   | 0
| "Charles" | 0
| "Bridget" | 1
| "Mats"    | 1
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===
--

The result shows that despite not having the `seedProperty` when it was created, the node 'Mats' has been assigned to the same component as the node 'Bridget'.
This is correct because these two nodes are connected.

[[algorithms-wcc-examples-seed-write]]
=== Writing Seeded components

In the <<algorithms-wcc-examples-seeding, previous section>> we demonstrated the `seedProperty` usage in `stream` mode.
It is also available in the other modes of the algorithm.
Below is an example on how to use `seedProperty` in `write` mode.
Note that the example below relies on <<algorithms-wcc-examples-seeding-steps, Steps 1 - 3>> from the previous section.

[role=query-example, group=seeding]
--
.The following will run the algorithm in `write` mode using `seedProperty`:
[source, cypher]
----
CALL gds.wcc.write('myGraph-seeded', {
  seedProperty: 'componentId',
  writeProperty: 'componentId',
  relationshipWeightProperty: 'weight',
  threshold: 1.0
})
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header"]
|===
| nodePropertiesWritten | componentCount
| 1                     | 3
|===
--

[NOTE]
====
If the `seedProperty` configuration parameter has the same value as `writeProperty`, the algorithm only writes properties for nodes where the component ID has changed.
If they differ, the algorithm writes properties for all nodes.
====
