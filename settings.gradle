pluginManagement {
    repositories {
        maven {
            url 'https://neo.jfrog.io/neo/docs-maven'
        }
        gradlePluginPortal()
    }
}

rootProject.name = 'graph-data-science'

def registerSubModules(File file) {
    def moduleDirectories = new LinkedList<File>()

    file.eachFileRecurse {f ->
        if (f.name == "build.gradle") {
            moduleDirectories.add(f.parentFile)
        }
    }

    moduleDirectories.stream()
            .filter { moduleDir -> moduleDir != new File("./") && moduleDir != new File("./public") }
            .filter { moduleDir -> moduleDir.name != 'pregel-bootstrap' }
            .filter { moduleDir -> moduleDir.getParentFile().name != "4.2" }
            .forEach { moduleDir ->
                def parent = moduleDir.getParentFile();
                def projectName = ":${moduleDir.name}";
                if (parent.name == "proc") {
                    projectName = ":proc-${moduleDir.name}";
                } else if (parent.name == "api") {
                    projectName = ":${moduleDir.name}-api";
                } else if (parent.name == "4.0") {
                    projectName = ":${moduleDir.name}-4.0";
                } else if (parent.name == "4.1") {
                    projectName = ":${moduleDir.name}-4.1";
                }
                include(projectName)
                project(projectName).projectDir = moduleDir
            }
}

registerSubModules(new File("./"))
